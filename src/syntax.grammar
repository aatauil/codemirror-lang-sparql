@detectDelim

@top Unit {
  ( Query | Update )
}

Query {
  Prologue ( SelectQuery | ConstructQuery | DescribeQuery | AskQuery ) ValuesClause    
}

Update {
  Prologue ( Update1 ( ';' Update )? )?
}

Update1 {
 ( Load | Clear | Drop | Add | Move | Copy | Create | InsertData | DeleteData | DeleteWhere | Modify )
}

Load {
  LOAD SILENT? Iri ( INTO GraphRef )?
}

Clear {
  CLEAR SILENT? GraphRefAll
}

Drop {
  DROP SILENT? GraphRefAll
}

Create {
  CREATE SILENT? GraphRef
}

Add {
  ADD SILENT? GraphOrDefault TO GraphOrDefault
}

Move {
  MOVE SILENT? GraphOrDefault TO GraphOrDefault
}

Copy {
  COPY SILENT? GraphOrDefault TO GraphOrDefault
}

InsertData {
  INSERT DATA QuadData
}

DeleteData {
  DELETE DATA QuadData
}

DeleteWhere {
  DELETE WHERE QuadPattern
}

Modify {
  ( WITH Iri )? ( DeleteClause InsertClause? | InsertClause ) UsingClause* WHERE GroupGraphPattern
}

GraphRef {
  GRAPH Iri
}

Prologue { 
  (BaseDecl | PrefixDecl)*
}

BaseDecl {
  BASE IriRef
}

PrefixDecl {
  PREFIX Namespace IriRef
}

SelectQuery {
  SelectClause DatasetClause* WhereClause SolutionModifier
}

SelectClause {
  SELECT ( DISTINCT | REDUCED )? ( ( Var | ( '(' Expression AS Var ')' ) )+ | '*' )
}

Expression {
  ConditionalOrExpression 
}

ConditionalOrExpression {
  ConditionalAndExpression ( '||' ConditionalAndExpression )*
}

ConditionalAndExpression {
  ValueLogical ( '&&' ValueLogical )*
}

ValueLogical {
  RelationalExpression
}

RelationalExpression {
  NumericExpression ( 
    '=' NumericExpression | 
    '!=' NumericExpression | 
    '<' NumericExpression | 
    '>' NumericExpression | 
    '<=' NumericExpression | 
    '>=' NumericExpression | 
    IN ExpressionList | 
    NOT IN ExpressionList 
  )?
}

NumericExpression {
  AdditiveExpression
}

AdditiveExpression {
  MultiplicativeExpression ( 
    '+' MultiplicativeExpression | 
    '-' MultiplicativeExpression | 
    ( NumericLiteralPositive |  NumericLiteralNegative ) 
    ( 
      ( '*' UnaryExpression ) | 
      ( '/' UnaryExpression ) 
    )* 
  )*
}

MultiplicativeExpression {
  UnaryExpression ( '*' UnaryExpression | '/' UnaryExpression )*
}

UnaryExpression {
    '!' PrimaryExpression
  | '+' PrimaryExpression
  | '-' PrimaryExpression
  | PrimaryExpression
}

PrimaryExpression {
  (
   BrackettedExpression | 
   BuiltInCall | 
   iriOrFunction | 
   RDFLiteral | 
   NumericLiteral | 
   BooleanLiteral | 
   Var
  )
}

BrackettedExpression {
  '(' Expression ')'
}

BuiltInCall {
  ( Aggregate
  | STR '(' Expression ')'
  | LANG '(' Expression ')'
  | LANGMATCHES '(' Expression ',' Expression ')'
  | DATATYPE '(' Expression ')'
  | BOUND '(' Var ')'
  | IRI '(' Expression ')'
  | URI '(' Expression ')'
  | BNODE ( '(' Expression ')' | Nil )
  | RAND Nil
  | ABS '(' Expression ')'
  | CEIL '(' Expression ')'
  | FLOOR '(' Expression ')'
  | ROUND '(' Expression ')'
  | CONCAT ExpressionList
  | SubstringExpression
  | STRLEN '(' Expression ')'
  | StrReplaceExpression
  | UCASE '(' Expression ')'
  | LCASE '(' Expression ')'
  | ENCODE_FOR_URI '(' Expression ')'
  | CONTAINS '(' Expression ',' Expression ')'
  | STRSTARTS '(' Expression ',' Expression ')'
  | STRENDS '(' Expression ',' Expression ')'
  | STRBEFORE '(' Expression ',' Expression ')'
  | STRAFTER '(' Expression ',' Expression ')'
  | YEAR '(' Expression ')'
  | MONTH '(' Expression ')'
  | DAY '(' Expression ')'
  | HOURS '(' Expression ')'
  | MINUTES '(' Expression ')'
  | SECONDS '(' Expression ')'
  | TIMEZONE '(' Expression ')'
  | TZ '(' Expression ')'
  | NOW Nil
  | UUID Nil
  | STRUUID Nil
  | MD5 '(' Expression ')'
  | SHA1 '(' Expression ')'
  | SHA256 '(' Expression ')'
  | SHA384 '(' Expression ')'
  | SHA512 '(' Expression ')'
  | COALESCE ExpressionList
  | IF '(' Expression ',' Expression ',' Expression ')'
  | STRLANG '(' Expression ',' Expression ')'
  | STRDT '(' Expression ',' Expression ')'
  | SAMETERM '(' Expression ',' Expression ')'
  | ISIRI '(' Expression ')'
  | ISURI '(' Expression ')'
  | ISBLANK '(' Expression ')'
  | ISLITERAL '(' Expression ')'
  | ISNUMERIC '(' Expression ')'
  | RegexExpression
  | ExistsFunc
  | NotExistsFunc )
}

Aggregate {
  ( COUNT '(' DISTINCT? ( '*' | Expression ) ')'
  | SUM '(' DISTINCT? Expression ')'
  | MIN '(' DISTINCT? Expression ')'
  | MAX '(' DISTINCT? Expression ')'
  | AVG '(' DISTINCT? Expression ')'
  | SAMPLE '(' DISTINCT? Expression ')'
  | GROUP_CONCAT '(' DISTINCT? Expression ( ';' SEPARATOR '=' String )? ')' )
}

String {
  stringLiteral1 |
  stringLiteral2 |
  stringLiteralLong1 |
  stringLiteralLong2
}

ExpressionList {
  ( Nil | "(" Expression ("," Expression)* ")" )
}

SubstringExpression {
  SUBSTR '(' Expression ',' Expression ( ',' Expression )? ')'
}

StrReplaceExpression {
  REPLACE '(' Expression ',' Expression ',' Expression ( ',' Expression )? ')'
}

RegexExpression {
  REGEX '(' Expression ',' Expression ( ',' Expression )? ')'
}

ExistsFunc {
  EXISTS GroupGraphPattern
}

NotExistsFunc {
  NOT EXISTS GroupGraphPattern
}

GroupGraphPattern {
  '{' ( SubSelect | GroupGraphPatternSub ) '}'
}

SubSelect {
  SelectClause WhereClause SolutionModifier ValuesClause
}

WhereClause {
  WHERE? GroupGraphPattern
}

SolutionModifier {
  GroupClause? HavingClause? OrderClause? LimitOffsetClauses?
}

GroupClause {
  GROUP BY GroupCondition+
}

GroupCondition {
  ( BuiltInCall | FunctionCall | '(' Expression ( AS Var )? ')' | Var )
}

FunctionCall {
  Iri ArgList
}

ArgList {
  ( Nil | '(' DISTINCT? Expression ( ',' Expression )* ')' )
}

HavingClause {
  HAVING HavingCondition+
}

HavingCondition {
  Constraint
}

Constraint {
  ( BrackettedExpression | BuiltInCall | FunctionCall )
}

OrderClause {
  ORDER BY OrderCondition+
}

OrderCondition {
  ( ( ASC | DESC ) BrackettedExpression ) | ( Constraint | Var )
}

LimitOffsetClauses {
  ( LimitClause OffsetClause? | OffsetClause LimitClause? )
}

LimitClause {
  LIMIT Integer+
}

OffsetClause {
  OFFSET Integer+
}

ValuesClause {
  ( VALUES DataBlock )?
}

DataBlock {
 ( InlineDataOneVar | InlineDataFull )
}

InlineDataOneVar {
  Var '{' DataBlockValue* '}'
}

InlineDataFull {
  ( Nil | '(' Var* ')' ) '{' ( '(' DataBlockValue* ')' | Nil )* '}'
}

DataBlockValue {
 ( Iri | RDFLiteral | NumericLiteral | BooleanLiteral | UNDEF )
}

RDFLiteral {
  String ( Langstag | ( '^^' Iri ) )?
}

NumericLiteral {
  ( NumericLiteralUnsigned | NumericLiteralPositive | NumericLiteralNegative )
}

NumericLiteralUnsigned {
  ( Integer | Decimal | Double )
}

NumericLiteralPositive {
  ( IntegerPositive | DecimalPositive | DoublePositive )
}

NumericLiteralNegative {
  ( IntegerNegative | DecimalNegative | DoubleNegative )
}

BooleanLiteral {
  ( TRUE | FALSE )
}

GroupGraphPatternSub {
  TriplesBlock? ( GraphPatternNotTriples '.'? TriplesBlock? )*
}

TriplesBlock {
  TriplesSameSubjectPath ( '.' TriplesBlock? )?
}

TriplesSameSubjectPath {
  VarOrTerm PropertyListPathNotEmpty | TriplesNodePath PropertyListPath
}

VarOrTerm {
  ( Var | GraphTerm )
}

GraphTerm {
  ( Iri | RDFLiteral | NumericLiteral | BooleanLiteral | BlankNode | Nil )
}

BlankNode {
  ( BlankNodeLabel | Anon )
}

BlankNodeLabel {
  "_:" BlankNodeValue?
}

PropertyListPathNotEmpty {
  ( VerbPath | VerbSimple ) ObjectListPath ( ';' ( ( VerbPath | VerbSimple ) ObjectList )? )*
}

VerbPath {
  Path
}

Path {
  PathAlternative
}

PathAlternative {
  PathSequence ( '|' PathSequence )*
}

PathSequence {
  PathEltOrInverse ( '/' PathEltOrInverse )*
}

PathElt {
  PathPrimary PathMod?
}

PathEltOrInverse {
  ( PathElt | '^' PathElt )
}

PathPrimary {
 ( Iri | 'a' | '!' PathNegatedPropertySet | '(' Path ')' )
}

PathNegatedPropertySet {
  ( PathOneInPropertySet | '(' ( PathOneInPropertySet ( '|' PathOneInPropertySet )* )? ')' )
}

PathOneInPropertySet {
 ( Iri | 'a' | '^' ( Iri | 'a' ) )
}

PathMod {
  ( '?' | '*' | '+' )
}

VerbSimple {
  Var
}

ObjectListPath {
  ObjectPath ( ',' ObjectPath )*
}

ObjectPath {
  GraphNodePath
}

GraphNodePath {
  ( VarOrTerm | TriplesNodePath )
}

TriplesNodePath {
  ( CollectionPath | BlankNodePropertyListPath )
}

BlankNodePropertyListPath {
  '[' PropertyListPathNotEmpty ']'
}

Collection {
  '(' GraphNode+ ')'
}

CollectionPath {
  '(' GraphNodePath+ ')'
}

ObjectList {
  Object ( ',' Object )* 
}

Object {
  GraphNode
}

GraphNode {
  ( VarOrTerm | TriplesNode )
}

TriplesNode {
  ( Collection | BlankNodePropertyList )
}

BlankNodePropertyList {
  '[' PropertyListNotEmpty ']'
}

PropertyListNotEmpty {
  Verb ObjectList ( ';' ( Verb ObjectList )? )*
}

Verb {
  ( VarOrIri | 'a' )
}

VarOrIri {
 ( Var | Iri )
}

PropertyListPath {
  PropertyListPathNotEmpty?
}

GraphPatternNotTriples {
	( GroupOrUnionGraphPattern | OptionalGraphPattern | MinusGraphPattern | GraphGraphPattern | ServiceGraphPattern | Filter | Bind | InlineData )
}

GroupOrUnionGraphPattern {
  GroupGraphPattern ( UNION GroupGraphPattern )*
}

OptionalGraphPattern {
  OPTIONAL GroupGraphPattern
}

MinusGraphPattern {
  MINUS GroupGraphPattern
}

GraphGraphPattern {
  GRAPH VarOrIri GroupGraphPattern
}

ServiceGraphPattern {
  SERVICE SILENT? VarOrIri GroupGraphPattern
}

Filter {
  FILTER Constraint
}

Bind {
  BIND '(' Expression AS Var ')'
}

InlineData {
  VALUES DataBlock
}

iriOrFunction {
  Iri ArgList?
}

DatasetClause {
  FROM ( DefaultGraphClause | NamedGraphClause )
}

DefaultGraphClause {
  SourceSelector
}

NamedGraphClause {
  NAMED SourceSelector
}

SourceSelector {
  Iri
}

ConstructQuery {
  CONSTRUCT ( ConstructTemplate DatasetClause* WhereClause SolutionModifier | DatasetClause* WHERE '{' TriplesTemplate? '}' SolutionModifier )
}

ConstructTemplate {
  '{' ConstructTriples? '}'
}

ConstructTriples {
  TriplesSameSubject ( '.' ConstructTriples? )?
}

TriplesSameSubject {
  VarOrTerm PropertyListNotEmpty | TriplesNode PropertyList
}

PropertyList {
  PropertyListNotEmpty?
}

TriplesTemplate {
  TriplesSameSubject ( '.' TriplesTemplate? )?
}

DescribeQuery {
  DESCRIBE ( VarOrIri+ | '*' ) DatasetClause* WhereClause? SolutionModifier
}

AskQuery {
  ASK DatasetClause* WhereClause SolutionModifier
}

GraphRefAll {
  ( GraphRef | DEFAULT | NAMED | ALL )
}

GraphOrDefault {
  DEFAULT | GRAPH? Iri
}

QuadData {
  '{' Quads '}'
}

Quads {
  TriplesTemplate? ( QuadsNotTriples '.'? TriplesTemplate? )*
}

QuadsNotTriples {
  GRAPH VarOrIri '{' TriplesTemplate? '}'
}

QuadPattern {
  '{' Quads '}'
}

DeleteClause {
  DELETE QuadPattern
}

InsertClause {
  INSERT QuadPattern
}

UsingClause {
  USING ( Iri | NAMED Iri )
}


@skip { space | Comment* }

@tokens {
  space { 
    @whitespace+ 
  }

  Comment { 
    "#" ![\n]* 
  }
  Keyword {
    $[_a-zA-Z\u00A1-\u00ff]+
  }

  IriRef {
    "<" (![<>"{}|^`\\\x00-\x20])* ">"
  }

  Namespace {
    PnPrefix* ":"
  }

  PnPrefix {
    $[a-z] | $[A-Z]
  }

  Var {
    $[?$] VarName
  }

  VarName {
    ( pn_chars_u | $[0-9] ) ( pn_chars_u | $[0-9] | $[\u00B7] | $[\u0300-\u036F] | $[\u203F-\u2040] )*
  }

  pn_chars_u {
    ( pn_chars_base | '_' )
  }

  pn_chars_base {
    $[A-Z] | $[a-z]
  }

  stringLiteral1 {
    "'" (![\x27\x5C\x0A\x0D] | Echar)* "'"
  }
  
  stringLiteral2 {
    "\"" (![\x22\x5C\x0A\x0D] | Echar)* "\""
  }
  
  stringLiteralLong1 {
    "'''" (("'" | "''")? (!['\\] | Echar))* "'''"
  }
  
  stringLiteralLong2 {
    "\"\"\"" (("\"" | "\"\"")? (!["\\] | Echar))* "\"\"\""
  }

  Echar {
    "\\\\" $[tbnrf\\"']
  }

  Nil {
    "(" ("\\x20" | "\\x09" | "\\x0D" | "\\x0A")* ")"
  }

  Iri {
    IriRef | PrefixedName
  }

  PrefixedName {
    PnameLn | Namespace
  }

  PnameLn {
    Namespace PnLocal
  }

  PnLocal {
    (pn_chars_u | "\\:" | $[0-9] | plx ) ((pn_chars | '.' | ':' | plx)* (pn_chars | ':' | plx) )?
  }

  plx {
    (percent | pn_local_esc)
  }

  percent {
    "%" hex hex
  }

  hex {
    $[0-9] | $[A-F] | $[a-f]
  }
  
  pn_local_esc {
    "\\"
  }

  pn_chars {
    ( pn_chars_u | '-' | $[0-9] | $[\u00B7] | $[\u0300-\u036F] | $[\u203F-\u2040] )
  }

  Integer {
    @digit+
  }

  Decimal {
    @digit* "." @digit+
  }

  Double {
    @digit+ "." @digit* Exponent | "." (@digit)+ Exponent | (@digit)+ Exponent
  }


  IntegerPositive {
    "+" Integer
  }

  DecimalPositive {
    "+" Decimal
  }

  DoublePositive {
    "+" Double
  }

  IntegerNegative {
    "-" Integer
  }

  DecimalNegative {
    "-" Decimal
  }

  DoubleNegative {
    "-" Double
  }

  Exponent {
    $[eE] $[+-]? @digit+
  }

  Langstag {
    "@" @asciiLetter+ ("-" ( @asciiLowercase | @asciiUppercase | @digit)+)*
  }

  BlankNodeValue {
   ( $[A-Z] | $[a-z] | $[\u00C0-\u00D6] | $[\u00D8-\u00F6] | $[\u00F8-\u02FF] | $[\u0370-\u037D] | $[\u037F-\u1FFF] | $[\u200C-\u200D] | $[\u2070-\u218F] | $[\u2C00-\u2FEF] | $[\u3001-\uD7FF] | $[\uF900-\uFDCF] | $[\uFDF0-\uFFFD] | "_" | $[0-9] )+
  }

  Anon {
    "[" ("\\x20" | "\\x09" | "\\x0D" | "\\x0A")* "]"
  }

  @precedence { Iri, "a" , "_:", space, Keyword,  BlankNodeValue }
  @precedence {
    DoublePositive, DecimalPositive, IntegerPositive, Double, Decimal, Integer, "+", ".", BlankNodeValue
  }

  @precedence {
    DoubleNegative, DecimalNegative, IntegerNegative, Double, Decimal, Integer, "-", ".", BlankNodeValue
  }

  @precedence {
    Var "?" 
  }

  "_:" "{" "}" "." "," ";"
}


@external specialize { Keyword } caseInsensitive from "./tokens" {
  BASE, PREFIX, SELECT, CONSTRUCT, DESCRIBE, WHERE, DISTINCT, REDUCED, STR, LANG, LANGMATCHES, DATATYPE, ASK, BOUND, IRI, URI, BNODE, RAND, ABS, CEIL, FLOOR, ROUND, CONCAT, STRLEN, UCASE, LCASE,
  ENCODE_FOR_URI, CONTAINS, STRSTARTS, STRENDS, STRBEFORE, STRAFTER, YEAR, MONTH, DAY, 
  HOURS, MINUTES, SECONDS, TIMEZONE, TZ, NOW, UUID, STRUUID, MD5, SHA1, SHA256, SHA384, 
  SHA512, COALESCE, IF, STRLANG, STRDT, SAMETERM, ISIRI, ISURI, ISBLANK, ISLITERAL, 
  ISNUMERIC, COUNT, SUM, MIN, MAX, AVG, SAMPLE, GROUP_CONCAT, SEPARATOR, SUBSTR, REPLACE,
  REGEX, EXISTS, NOT, IN, GROUP, BY, HAVING, ORDER, ASC, DESC, LIMIT, OFFSET, VALUES, UNDEF,
  UNION, OPTIONAL, MINUS, GRAPH, SERVICE, SILENT, FILTER, BIND, AS, FROM, NAMED, TRUE, FALSE, LOAD, INTO, TO, CLEAR, DROP, CREATE, ADD, MOVE, COPY, INSERT, DATA, DELETE, WITH, DEFAULT, ALL, USING
}

